# Process this file with autoconf to produce a configure script.
PACKAGE=libtscb

AC_INIT(src, 0.9.9, hcb@chaoticmind.net)
AC_CONFIG_SRCDIR(.)

VERSION=0.9.9

AC_CANONICAL_HOST
case "$host_os" in
	darwin*) PORTNAME=darwin ;;
	linux*) PORTNAME=linux ;;
	freebsd*) PORTNAME=freebsd ;;
esac
AC_SUBST(PORTNAME)

AC_CONFIG_HEADERS([tscb/config.h:tscb/config.h.in])

AC_PROG_CXX
AC_PROG_INSTALL

# Check for system features
AC_SYS_LARGEFILE
if test "$ac_cv_sys_file_offset_bits" != "" -a "$ac_cv_sys_file_offset_bits" != "no"; then
  CPPFLAGS="$CPPFLAGS -D_FILE_OFFSET_BITS=$ac_cv_sys_file_offset_bits"
fi

# Checks for libraries.
ACX_PTHREAD

AC_CHECK_LIB(aio, io_setup)
AC_CHECK_LIB(rt, aio_suspend)

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([sys/poll.h sys/event.h sys/epoll.h libaio.h])

# Checks for library functions.
AC_CHECK_FUNCS([gettimeofday])
AC_SEARCH_LIBS([clock_gettime],[rt posix4])

AC_CHECK_FUNC(poll,[
AC_DEFINE(HAVE_POLL,1,[Define if you have the poll system call.])
DISPATCHER_POLL=yes
],[])

AC_CHECK_FUNC(select,[
AC_DEFINE(HAVE_SELECT,1,[Define if you have the select system call.])
DISPATCHER_SELECT=yes
],[])

AC_CHECK_FUNC(epoll_create,[
AC_DEFINE(HAVE_EPOLL,1,[Define if you have the epoll_create system call.])
DISPATCHER_EPOLL=yes
],[])

AC_CHECK_FUNC(epoll_create1,[
AC_DEFINE(HAVE_EPOLL1,1,[Define if you have the epoll_create1 system call.])
DISPATCHER_EPOLL=yes
],[])

AC_CHECK_FUNC(pipe2,[
AC_DEFINE(HAVE_PIPE2,1,[Define if you have the pipe2 system call.])
],[])

AC_CHECK_FUNC(kqueue,[
AC_DEFINE(HAVE_KQUEUE,1,[Define if you have the kqueue system call.])
DISPATCHER_KQUEUE=yes
],[])

AC_CHECK_FUNC(inotify_init1,[
AC_DEFINE(HAVE_INOTIFY,1,[Define if you have the inotify set of sstem calls.])
INOTIFY=yes
],[])

AC_ARG_ENABLE(no-shared,[  --enable-no-shared        Disable shared library build of libtscb],[ENABLE_SHARED=no],[ENABLE_SHARED=yes])

AC_ARG_ENABLE(coverage,[  --enable-coverage       Enable test coverage computation],[ENABLE_COVERAGE=yes],[])

AC_SUBST(DISPATCHER_POLL)
AC_SUBST(DISPATCHER_SELECT)
AC_SUBST(DISPATCHER_EPOLL)
AC_SUBST(DISPATCHER_KQUEUE)
AC_SUBST(INOTIFY)
AC_SUBST(VERSION)
AC_SUBST(ENABLE_SHARED)
AC_SUBST(ENABLE_COVERAGE)

# Generate output files
AC_CONFIG_FILES([Makefile])
AC_CONFIG_FILES([libtscb.pc])
AC_OUTPUT
